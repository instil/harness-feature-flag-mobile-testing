apply plugin: "com.android.model.library"
apply plugin: "jacoco"
apply from: "${project.rootDir}/gradle/static-analysis.gradle"

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.3"

        defaultConfig {
            minSdkVersion.apiLevel 19
            targetSdkVersion.apiLevel 23
            versionCode 1
            versionName "1.0"
        }
    }

    android.sources {
        main {
            jni {
                source {
                    srcDirs = ["../../core/src",
                               "../../core/lib/libuv-cmake/libuv/src",
                               "../../core/lib/uvw/src",
                               "src/main/jni"]

                    exclude "**/aix.c"
                    exclude "**/darwin-proctitle.c"
                    exclude "**/darwin.c"
                    exclude "**/freebsd.c"
                    exclude "**/fsevents.c"
                    exclude "**/kqueue.c"
                    exclude "**/netbsd.c"
                    exclude "**/openbsd.c"
                    exclude "**/os390.c"
                    exclude "**/sunos.c"
                    exclude "**/win/**"
                }
                exportedHeaders {
                    srcDirs = ["../../core/lib/libuv-cmake/libuv/include",
                               "../../core/lib/libuv-cmake/libuv/src",
                               "../../core/lib/uvw/src"]
                }
            }
        }
    }

    android.ndk {
        moduleName "surge"
        cppFlags.add("-std=c++14")
        cppFlags.add("-fexceptions")
        cppFlags.add("-I" + file("../../core/src"))
        ldLibs.addAll(["log"])
        stl "c++_shared"
        toolchain "clang"
    }

    android.buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled = false
            proguardFiles.add(file("proguard-rules.pro"))
        }
    }
}

ext.includeLibsFolder = 'libs'
task copyLibs(type: Copy, dependsOn: 'cleanLibs') {
    from configurations.compile
    into includeLibsFolder
}

task cleanLibs(type: Delete) {
    delete includeLibsFolder
}

dependencies {
    compile fileTree(dir: includeLibsFolder, include: ["*.jar"])
    compile "com.android.support:appcompat-v7:23.4.0"
    compile "org.slf4j:slf4j-api:1.7.21"

    testCompile "junit:junit:4.12"
    testCompile "org.easymock:easymock:3.4"
}
